///$tab Main
SET ThousandSep=',';
SET DecimalSep='.';
SET MoneyThousandSep=',';
SET MoneyDecimalSep='.';
SET MoneyFormat='£#,##0.00;-£#,##0.00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET MonthNames='Jan;Feb;Mar;Apr;May;Jun;Jul;Aug;Sep;Oct;Nov;Dec';
SET DayNames='Mon;Tue;Wed;Thu;Fri;Sat;Sun';
SET LongMonthNames='January;February;March;April;May;June;July;August;September;October;November;December';
SET LongDayNames='Monday;Tuesday;Wednesday;Thursday;Friday;Saturday;Sunday';
SET FirstWeekDay=0;
SET BrokenWeeks=0;
SET ReferenceDay=4;
SET FirstMonthOfYear=1;
SET CollationLocale='en-GB';

RawData:
Load * Inline [
	Pat_ID,Date,TreatDataTableInfo,TreatId
	PatNum_12,08.07.2016,HDF Pradilution,1
	PatNum_12,07.07.2016,HDF Predilution,2
	PatNum_12,23.03.2016,HD,3
	PatNum_12,24.11.2015,HD,4
	PatNum_12,22.11.2015,HD,5
	PatNum_12,04.09.2015,HD,6
	PatNum_12,01.09.2015,HD,7
	PatNum_12,30.07.2015,HD,8
	PatNum_12,12.01.2015,HD,9
	PatNum_12,09.01.2015,HD,10
	PatNum_12,26.08.2014,Hemodialysis,11
	PatNum_12,08.07.2014,Hemodialysis,12
	PatNum_12,23.05.2014,Hemodialysis,13
	PatNum_12,19.03.2014,Hemodialysis,14
	PatNum_12,29.01.2014,Hemodialysis,15
	PatNum_12,14.12.2013,Hemodialysis,16
	PatNum_12,26.10.2013,Hemodialysis,17
	PatNum_12,05.10.2013,Hemodialysis,18
	PatNum_12,03.10.2013,HD,19
	PatNum_12,24.06.2013,Hemodialysis,20
	PatNum_12,03.06.2013,Hemodialysis,21
	PatNum_12,14.05.2013,Hemodialysis,22
	PatNum_12,26.02.2013,HDF Postdilution,23
	PatNum_12,23.02.2013,HDF Pradilution,24
	PatNum_12,21.02.2013,HDF Postdilution,25
	PatNum_12,07.02.2013,HD,26
	PatNum_12,25.01.2013,HDF Pradilution,27
	PatNum_12,18.01.2013,HDF Pradilution,28
];


GroupedData:
Load
	*,
	// assign new GroupId for all rows where the TreatDataTableInfo is equal
	if( RowNo() = 1, 1,
		if( TreatDataTableInfo <> peek('TreatDataTableInfo'),
		    peek('GroupId') + 1, peek('GroupId'))) as GroupId,	
	// assign new GroupSubId (incremental int) for all the records in each group
	if( TreatDataTableInfo <> peek('TreatDataTableInfo'), 
	    1, peek('GroupSubId') + 1) as GroupSubId,
	// pick the first Date field value and spread it acccross the group
	if( TreatDataTableInfo <> peek('TreatDataTableInfo'), TreatId, peek('TreatId_Temp')) as TreatId_Temp
Resident
	RawData
;

Drop Table RawData;	


right join (GroupedData)

// get the max GroupSubId for each group and right join it to 
// the GroupedData table to remove the records we dont need
MaxByGroup:
Load
	max(GroupSubId) as GroupSubId,
	GroupId
Resident
	GroupedData
Group By
	GroupId	
;		
	
// these are not needed anymore	
Drop Fields GroupId, GroupSubId, TreatId;

// replace the old TreatId with the new TreatId_Temp field
// which contains the first TreatId for each group
Rename Field TreatId_Temp to TreatId; 